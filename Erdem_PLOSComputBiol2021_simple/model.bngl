# CMM for MCF7 cell line
# 03/01/18 by Cemal ERDEM
#
# This is the BioNetGen model of early-stage signaling through 
# InsR and IGF1R. Parameters are fit to RPPA data from MCF7 cells 
# as described in the main text. 
# 
# To Run: Change the file extension from '.txt' to '.bngl' and 
# use as input to BioNetGen. Details on BioNetGen can be found 
# at bionetgen.org
# 
# MODEL details:
# 1) IGF1-IGF1R and insulin-InsR binding only
# 2) ONE phospho sites at each receptor 
# 3) NO basal phosphorylation
# 4) Rate parameters are in log10 
# 5) FOUR observables

begin model 
begin parameters
	IGF1_0 1e5
	INS_0 0
	IGF1R_0 25000.0
	INSR_0 25000.0
	IRS_0 92766.0
	SOS_0 90075.0
	RAS_0 230642.0
	RAF_0 126069.0
	MEK_0 1098164.0
	ERK_0 763172.0
	PI3K_0 64009.0
	PDK1_0 186081.0
	AKT_0 432907.0
	TSC2_0 131339.0
	MTOR_0 83469.0
	RPS6K_0 121978.0
# Ligand-receptor interactions
	kf1		0.4837
	kf1b	-2.9153
	kf1c	2.9865
	kf1d	1.2052
	kf2		4.6312
	kf2b	-0.8667
	kf2c	4.8758
	kf2d	-2.6526
# Downstream interactions	
	kf3		-2.7913
	kf4		-3.1902
	kf5		-0.6920
	kf6		4.1250
	kf7		-3.0400
	kf8		-4.5760
	kf9		0.3532
	kf10	4.3309	
	kf11	-6.9315
	kf12	-3.8016
	kf13	-6.2483
	kf14	1
	kf15	-6.7787
	kf16	-7.7887
	kf17	-7.7124
# -P rates
	kf101	-0.0831
	kf102	-6.4728
	kf103	0.0477
	kf104	3.9039
	kf105	0.7593
	kf106	0.5137
	kf107	-0.8678
	kf108	-3.3447
	kf109	-4.5098
	kf110	6.3930
	kf111	-2.7962
	kf112	-2.8996
# Feedback events
	kf201	2.7628
	kf202	4.0772
	kf203	-5.4445
	kf204	0.2274
	kf206	-6.3512
	kf207	-6.3752
	kf208	-5.0059
# Re-sensitization events	
	kf301	-3.0943
	kf302	-0.9807
	kf303	4.2786
	kf304	-5.6559
## Recepter recycling		
	kf401	-3.1016
	kf402	-2.9249
	kf403	-3.4193
	kf404	-7.0807
end parameters


begin molecule types
	# 16 species
	IGF1(rec)
	Ins(rec)
	IGF1R(lig,phos~U~P,int~N~Y)
	InsR(lig,phos~U~P,int~N~Y)
	IRS(phos~U~P,inh~N~Y)
	SOS(act~N~Y,inh~N~Y)
	Ras(gtp~N~Y)
	Raf(phos~U~P,inh~N~Y)
	MEK(phos~U~P,inh~N~Y)
	PI3K(act~N~Y)
	PDK1(act~N~Y)
	TSC2(phos~U~P)
    mTOR(act~N~Y)
    Akt(phos~U~P)
    RPS6K(phos~U~P)
    ERK(phos~U~P)
end molecule types
 
begin seed species
	IGF1(rec) IGF1_0
	Ins(rec) INS_0
	IGF1R(lig,phos~U,int~N) IGF1R_0
	InsR(lig,phos~U,int~N) INSR_0
	IRS(phos~U,inh~N) IRS_0
	SOS(act~N,inh~N) SOS_0
	Ras(gtp~N) RAS_0
	Raf(phos~U,inh~N) RAF_0
	MEK(phos~U,inh~N) MEK_0
	PI3K(act~N) PI3K_0
	PDK1(act~N) PDK1_0
	TSC2(phos~U) TSC2_0
    mTOR(act~N) MTOR_0
    Akt(phos~U) AKT_0
    RPS6K(phos~U) RPS6K_0
	ERK(phos~U) ERK_0
end seed species

begin observables
	Species		pRecTot_free		IGF1R(int~N,phos~P),InsR(int~N,phos~P)
	Species		pAkt308_free		Akt(phos~P)
	Species		pRPS6K_free			RPS6K(phos~P) 
	Species		pERK_free			ERK(phos~P)
end observables

begin reaction rules
	# Initial ligand-receptor binding     
    IGF1(rec) + IGF1R(lig,int~N,phos~U)  <->  IGF1(rec!1).IGF1R(lig!1,int~N,phos~U) 	10^kf1,10^kf1b
	IGF1R(lig!+,int~N,phos~U) -> IGF1R(lig!+,int~N,phos~P)		10^kf1c
	IGF1R(lig!+,int~N,phos~P) -> IGF1R(lig!+,int~N,phos~U)		10^kf1d  # Corrected by PFL
    Ins(rec) + InsR(lig,int~N,phos~U)  <->  Ins(rec!1).InsR(lig!1,int~N,phos~U) 	10^kf2,10^kf2b
    InsR(lig!+,int~N,phos~U) -> InsR(lig!+,int~N,phos~P)	10^kf2c
 	InsR(lig!+,int~N,phos~P) -> InsR(lig!+,int~N,phos~U)		10^kf2d  # Corrected by PFL
    
    # pReceptor-IRS binding and activation
    IGF1R(int~N,phos~P) + IRS(inh~N,phos~U) -> IGF1R(int~N,phos~P) + IRS(inh~N,phos~P)		10^kf3
    InsR(int~N,phos~P) + IRS(inh~N,phos~U) -> InsR(int~N,phos~P) + IRS(inh~N,phos~P)		10^kf4
    # pReceptor-SOS binding and activation
    IGF1R(int~N,phos~P) + SOS(inh~N,act~N) -> IGF1R(int~N,phos~P) + SOS(inh~N,act~Y)		10^kf5
    InsR(int~N,phos~P) + SOS(inh~N,act~N) -> InsR(int~N,phos~P) + SOS(inh~N,act~Y)			10^kf6
              
    # SOS activation by IRS1
	IRS(inh~N,phos~P) + SOS(inh~N,act~N) -> IRS(inh~N,phos~P) + SOS(inh~N,act~Y)		10^kf7
    # Ras activation by SOS
    SOS(inh~N,act~Y) + Ras(gtp~N) -> SOS(inh~N,act~Y) + Ras(gtp~Y)	10^kf8
    # Raf activation by Ras
    Ras(gtp~Y) + Raf(inh~N,phos~U) -> Ras(gtp~Y) + Raf(inh~N,phos~P)		10^kf9
    # MEK activation by Raf
    Raf(inh~N,phos~P) + MEK(inh~N,phos~U) -> Raf(inh~N,phos~P) + MEK(inh~N,phos~P)		10^kf10
    # ERK activation by MEK
    MEK(inh~N,phos~P) + ERK(phos~U) -> MEK(inh~N,phos~P) + ERK(phos~P)		10^kf11

    # PI3K activation by IRS1
    IRS(inh~N,phos~P) + PI3K(act~N) -> IRS(inh~N,phos~P) + PI3K(act~Y)		10^kf12
    # PDK1 activation PI3K
    PI3K(act~Y) + PDK1(act~N) -> PI3K(act~Y) + PDK1(act~Y)		10^kf13
    # Akt activation by PDK1
    PDK1(act~Y) + Akt(phos~U) -> PDK1(act~Y) + Akt(phos~P)		10^kf14
    # TSC2 inactivation by Akt
    Akt(phos~P) + TSC2(phos~U) -> Akt(phos~P) + TSC2(phos~P)		10^kf15
    # mTOR activation by inactive TSC2
    TSC2(phos~P) + mTOR(act~N) -> TSC2(phos~P) + mTOR(act~Y) 		10^kf16
    # RPS6K activation by mTOR
    mTOR(act~Y) + RPS6K(phos~U) -> mTOR(act~Y) + RPS6K(phos~P)		10^kf17
     
    # De-phosphorylation (-P) events
    IRS(phos~P) -> IRS(phos~U)		10^kf101
    SOS(act~Y) -> SOS(act~N)		10^kf102
    Ras(gtp~Y) -> Ras(gtp~N)		10^kf103
    Raf(phos~P) -> Raf(phos~U)		10^kf104
    MEK(phos~P) -> MEK(phos~U)		10^kf105
    PI3K(act~Y) ->	PI3K(act~N)		10^kf106
    PDK1(act~Y) -> PDK1(act~N)		10^kf107
    TSC2(phos~P) -> TSC2(phos~U)	10^kf108
    mTOR(act~Y) -> mTOR(act~N)		10^kf109
    Akt(phos~P) -> Akt(phos~U)		10^kf110
    RPS6K(phos~P) -> RPS6K(phos~U)	10^kf111
    ERK(phos~P) -> ERK(phos~U)		10^kf112

	#### Negative feedbacks
    # SOS inactivation by pERK
    ERK(phos~P) + SOS(inh~N,act~N) -> ERK(phos~P) + SOS(inh~Y,act~N)		10^kf201
 	# MEK inactivation by pERK
    ERK(phos~P) + MEK(inh~N,phos~U) -> ERK(phos~P) + MEK(inh~Y,phos~U)		10^kf202
    # IRS1 inhibition by pRPS6K
    RPS6K(phos~P) + IRS(inh~N,phos~U) -> RPS6K(phos~P) + IRS(inh~Y,phos~U)	10^kf203
    # Raf inactivation by pAkt
    Akt(phos~P) + Raf(inh~N,phos~U) -> Akt(phos~P) + Raf(inh~Y,phos~U)		10^kf204     
    # IRS inactivation by pERK
    ERK(phos~P) + IRS(inh~N,phos~U) -> ERK(phos~P) + IRS(inh~Y,phos~U) 	10^kf206
    # Akt inactivation by pERK 
    ERK(phos~P) + Akt(phos~P) -> ERK(phos~P) + Akt(phos~U) 	10^kf207
    # IRS inactivation by pAkt
    Akt(phos~P) + IRS(inh~N,phos~U) -> Akt(phos~P) + IRS(inh~Y,phos~U)		10^kf208 

	#### Re-sensitization
 	IRS(inh~Y) -> IRS(inh~N)		10^kf301
    SOS(inh~Y) -> SOS(inh~N)		10^kf302
    Raf(inh~Y) -> Raf(inh~N)		10^kf303
    MEK(inh~Y) -> MEK(inh~N)		10^kf304
    
    ## Recepter recycling
    IGF1R(lig!+,int~N,phos~P) -> IGF1R(lig!+,int~Y,phos~P)		10^kf401
    IGF1(rec!1).IGF1R(lig!1,int~Y,phos~P) -> IGF1R(lig,int~N,phos~U)		10^kf402
    InsR(lig!+,int~N,phos~P) -> InsR(lig!+,int~Y,phos~P)		10^kf403
    Ins(rec!1).InsR(lig!1,int~Y,phos~P) -> InsR(lig,int~N,phos~U)			10^kf404	
    	
end reaction rules
end model

# actions
begin actions
generate_network({overwrite=>1})
#simulate({method=>"ode",t_end=>172800,sample_times=>[0,300,600,1800,21600,86400,172800]}) 
#simulate({method=>"ode",t_end=>2e5}) 
simulate({method=>"ode",t_start=>0,t_end=>1800,n_steps=>2e4})
#writeMexfile({t_start=>0,t_end=>2e5,n_steps=>10001,atol=>1e-6,rtol=>1e-9,max_num_steps=>10000,max_err_test_fails=>20})
#writeMfile_all()
writeLatex()
#writeXML()
writeSBML()  # Modified by PFL

end actions
